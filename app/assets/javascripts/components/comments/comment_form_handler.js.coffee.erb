class @FR2.CommentFormHandler
  constructor: ($formWrapper)->
    @formWrapper = $formWrapper

    @commentFormStore = new FR2.CommentFormStore this
    @commentFormLoadHandler = new FR2.CommentFormLoadHandler this
    @commentFormSubmissionHandler = new FR2.CommentFormSubmissionHandler this
    @commentFormFileUploader = new FR2.CommentFormFileUploader this
    @commentFormSuccessHandler = new FR2.CommentFormSuccessHandler this

    @startComment()

    if window.location.hash == "#open-comment"
      @openCommentFromUrlHash()

  commentFormReady: ->
    @removeEvents()

    @commentForm.initialize()
    @commentFormFileUploader.initialize()
    @commentFormStore.addStorageEvents()

    @addEvents()
    @decorate()

  successPageReady: ->
    @commentFormSuccessHandler.initialize()

  removeEvents: ->
    @formWrapper.unbind()

  addEvents: ->
    @previewComment()
    @startCommentOver()

  decorate: ->
    @addPublicIcons()
    @addTooltipToPublicIcons()
    @improveUploadButton()

  commentFormEl: ->
    @commentForm.commentFormEl()

  previewComment: ->
    @commentFormEl().on 'click', '.comment_preview', (e)=>
      e.preventDefault()

      fields = @commentForm.parseFields()
      html = @renderCommentSummary fields

      Handlebars.registerPartial 'comment_summary', $('#comment-summary-template').html()

      modalTitle    = 'Preview your comment'
      modalTemplate = $('#comment-summary-template')
      modalData     = fields

      compiledTemplate = Handlebars.compile modalTemplate.html()
      modalHtml        = compiledTemplate({
        fields: modalData
      })

      source = $('#comment-summary-template').html()
      template = Handlebars.compile source

      FR2.Modal.displayModal(
        modalTitle,
        modalHtml,
        {modalClass: "comment-preview-modal"}
      )

  renderCommentSummary: (fields)->
    source = $('#comment-summary-template').html()
    template = Handlebars.compile source
    template fields

  addPublicIcons: ->
    items = @commentFormEl().find '.inputs li'

    _.each items, (li)->
      $li = $(li)
      if $li.hasClass 'public'
        el = $li
          .find ' > :input'
          .last()

        if el.prop('id') == 'comment_general_comment' || el.prop('id') == 'fileupload'
          tooltipGravity = 'e'
        else
          tooltipGravity = 'w'

        el
          .after(
            $('<span>')
              .addClass 'public icon-fr2 icon-fr2-globe'
              .data 'tooltip-gravity', tooltipGravity
          )

  addTooltipToPublicIcons: ->
    _.each @commentFormEl().find('.inputs span.public'), (el)->
      el = $(el)

      tooltip = el.siblings('input').data('tooltip')
      if tooltip?
        el.data('tooltip', tooltip)
      else
        el.data('tooltip', "Information entered in this field will be publically viewable on Regulations.gov")

      className = "form-tooltip #{el.siblings('input').prop('type')}"
      tooltipPosition = if el.data('tooltip-gravity') == 'e' then 'centerLeft' else 'centerRight'
      tooltipHorizontalOffset = if el.data('tooltip-gravity') == 'e' then -10 else 5

      CJ.Tooltip.addFancyTooltip(
        $(el),
        {
          fade: true
          className: className
          opacity: 1
        },
        {
          horizontalOffset: tooltipHorizontalOffset
          position: tooltipPosition
          verticalOffset: -5
        }
      )

  fileUploadButton: ->
    @commentFormEl().find '#fileupload'

  fileUploadButtonWrapper: ->
    @commentFormEl().find '.fileupload-button'

  improveUploadButton: ->
    addFileButton = $('<div>')
    addFileButton
      .addClass 'add_file button'
      .text 'Add a file'
      .prepend(
        $('<span>').addClass 'icon-fr2 icon-fr2-add'
      )

    @fileUploadButtonWrapper().append addFileButton

    @fileUploadButton().on 'hover', ()=>
      addFileButton.toggleClass('hover');

  loadCommentForm: ->
    @commentFormLoadHandler.load()

  openCommentFromUrlHash: ->
    # we want the dom element so we can check for existence rather than
    # a jquery empty set []
    link = @formWrapper.find('a#start_comment[data-comment=1]').get(0)

    if link
      $(link).trigger 'click'
    else
      modalTitle = "Comment submission not available"
      modalHtml = "This document is not open for comment or is not available for comment submission on FederalRegister.gov. You may want to check the DATES, ADDRESSES, or FOR FURTHER INFORMATION CONTACT sections of this document."
      FR2.Modal.displayModal modalTitle, modalHtml

  submitCommentForm: ->
    @commentFormStore.storeComment()
    @trackCommentFormSubmitStart()
    @commentFormSubmissionHandler.submit()

  startComment: ->
    @formWrapper.on 'click', 'a#start_comment[data-comment=1]', (e)=>
      e.preventDefault()

      window.location.hash = '#open-comment'
      @trackCommentFormOpenStart()
      @loadCommentForm()

  trackCommentFormOpenStart: ->
    @trackCommentEvent 'Comment: Open Comment Form Start'

  trackCommentFormStartOver: ->
    @trackCommentEvent 'Comment: Comment Form Start Over'

  trackCommentFormSubmitStart: ->
    @trackCommentEvent 'Comment: Submit Comment Form Start'

  trackCommentEvent: (category)->
    wrapper = $('#comment-bar')

    agency = wrapper.data('reggov-agency')
    documentNumber = wrapper.data('document-number')

    FR2.Analytics.trackCommentEvent category, agency, documentNumber

  startCommentOver: ->
    @commentFormEl().on 'click', 'a#comment-start-over', (e)=>
      e.preventDefault()

      @trackCommentFormStartOver()

      @commentFormStore.clearSavedFormState()

      ajaxCommentData = $('.ajax-comment-data')
      commentWrapper = ajaxCommentData.find '.comment_wrapper'

      ajaxCommentData
        .animate {height: '42px'}, 400
      ajaxCommentData
        .scrollintoview {duration: 400}

      commentWrapper.slideUp 400, =>
        commentWrapper.remove()
        @loadCommentForm()

  updateCommentHeader: ->
    flashMessage = $('#comment-bar.comment p')

    flashMessage
      .find '.button'
      .fadeOut 400, ()->
        flashMessage
          .closest 'div'
          .addClass 'open'

        flashMessage
          .html '<div class="byline">You are submitting an official comment to Regulations.gov. <br> Comments are due ' + $('.comment_form_wrapper').data('comment-due') + '.</div> <img alt="Regulations.gov Logo" src="<%= asset_path('regulations_dot_gov_logo.png') %>" class="reg_gov_logo">'

  storeComment: ->
    @commentFormStore.storeComment()

  addStorageEvents: ->
    @commentFormStore.addStorageEvents()
