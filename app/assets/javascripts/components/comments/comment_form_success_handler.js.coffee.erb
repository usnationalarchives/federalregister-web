class @FR2.CommentFormSuccessHandler
  constructor: (commentFormHandler)->
    @commentFormHandler = commentFormHandler

  initialize: ->
    @add_events()
    @updateCommentHeader()

  formWrapper: ->
    @commentFormHandler.formWrapper

  commentWrapper: ->
    @commentFormHandler.commentWrapper()

  add_events: ->
    @commentNotifications()
    @printComment()
    @analytics()

  updateCommentHeader: ->
    flashMessage = $('#comment-bar.comment p')

    flashMessage
      .closest 'div'
      .removeClass 'open'

    flashMessage
      .html 'You have successfully submitted an official comment to Regulations.gov. <img alt="Regulations.gov Logo" src="<%= asset_path('regulations_dot_gov_logo.png') %>" class="reg_gov_logo">'

  commentNotifications: ->
    @commentWrapper().on 'click', '.action.commit', (e)->
      e.preventDefault()
      $(this).closest('form').submit()

    @commentWrapper().on 'click', '.my_fr a.notifications', (e)->
      e.preventDefault()
      e.stopPropagation()

      link = $(this)

      $.ajax({
        url: link.attr('href')
        dataType: 'json'
        type: link.data('method')
        data: {
          comment_tracking_number: link.data('comment-tracking-number')
        }
        success: (response)->
          link.html(response.link_text)
          link.data('method', response.method)
          link.closest('li').find('.description').html(response.description)
          link.toggleClass('remove')
      })

  printComment: ->
    $('.comment_wrapper').on 'click', 'a#print-comment', (e) ->

      e.preventDefault()

      printModalLink = $(this)

      modalTitle    = 'Print your comment'
      modalTemplate = $("#comment-print-summary-template")
      modalData     = printModalLink.data('comment-data')

      modalHtml = Handlebars.compile(modalTemplate.html())({
        fields: modalData
        document_details: printModalLink.data 'current-document-details'
        comment_details: printModalLink.data 'comment-details'
        agency_participating: printModalLink.data('comment-details').agency_participating
      })

      FR2.Modal.displayModal modalTitle, modalHtml, {modalClass: "print_modal"}

    $('body').on 'modalClose', (e) ->
      $('#main').removeClass('cj-hidden')
      $('#footer').removeClass('cj-hidden')

    $('body').on 'click', '.print_modal .print_button', (e)->
      e.preventDefault()
      $('#main').addClass('cj-hidden')
      $('#footer').addClass('cj-hidden')
      $('#fr_modal').addClass 'visible'
      window.print()

  trackCommentEvent: (category)->
    @commentFormHandler.trackCommentEvent category

  analytics: ->
    @commentWrapper().on 'click', '.tracking_number', ()=>
      @trackCommentEvent 'Comment Success: Comment Tracking Number'

    @commentWrapper().on 'click', '.my_fr .notifications.posting.remove', ()=>
      @trackCommentEvent 'Comment Success: MyFR Posting Notification Opt Out'

    @commentWrapper().on 'click', '.my_fr .notifications.posting', ()=>
      @trackCommentEvent 'Comment Success: MyFR Posting Notification Opt In'

    @commentWrapper().on 'click', '.my_fr .notifications.followup.remove', ()=>
      @trackCommentEvent 'Comment Success: MyFR Posting Notification Opt Out'

    @commentWrapper().on 'click', '.my_fr .notifications.followup', ()=>
      @trackCommentEvent 'Comment Success: MyFR Posting Notification Opt In'

    @commentWrapper().on 'click', '#print-comment', ()=>
      @trackCommentEvent 'Comment Success: Print Comment'

    @commentWrapper().on 'click', '.social_media .twitter', ()=>
      @trackCommentEvent 'Comment Success: Social Media Twitter'

    @commentWrapper().on 'click', '.social_media .facebook', ()=>
      @trackCommentEvent 'Comment Success: Social Media Facebook'

    @commentWrapper().on 'click', '.warning.message .resend_email_confirmation', ()=>
      @trackCommentEvent 'Comment Success:  MyFR Resend Email Confirmation'

    @commentWrapper().on 'click', '.actions .comment_form_sign_in', ()=>
      @trackCommentEvent 'Comment Success:  MyFR Sign In'

    @commentWrapper().on 'click', '.actions .comment_form_sign_up', ()=>
      @trackCommentEvent 'Comment Success:  MyFR Sign Up'
